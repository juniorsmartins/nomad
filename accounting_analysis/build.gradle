plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.0'
	id 'io.spring.dependency-management' version '1.1.6'
}

description = 'Desenvolve análises de dados a partir do livro-caixa.'
group = 'com.nomad'
version = '0.0.1-SNAPSHOT'

allprojects {

	repositories {
		mavenCentral()
	}

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(21)
		}
	}
}

subprojects {

	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}


// Manter temporariamente - descartar assim que possível
dependencies {
	// Spring Web
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// Processamento de anotações e configuração no Spring Boot - gera metadados de configuração
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
//	implementation 'org.springframework.boot:spring-boot-configuration-processor:3.4.2'

	// Spring Webflux
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Conversão MapStruct
	implementation 'org.mapstruct:mapstruct:1.6.3'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

	// Monitoramento e Observabilidade
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus:1.13.2'

	// Resilience4j - CircuitBreaker, BulkHead, RateLimiter, Retry, Cache e TimeLimiter
	implementation "io.github.resilience4j:resilience4j-spring-boot3:2.3.0"
	implementation "org.springframework.boot:spring-boot-starter-aop"
	implementation "io.github.resilience4j:resilience4j-reactor:2.3.0"

	// Tests
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

